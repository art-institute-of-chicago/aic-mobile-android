
buildscript {
    repositories {
        google()
        jcenter()
        maven {
            url 'https://maven.fabric.io/public'
        }
    }
    dependencies {
        classpath 'com.google.gms:google-services:4.1.0'
    }
}

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'

String buildMetadata

if (project.hasProperty('build_number')) {
    /**
     * This is typically the build number produced by the CI service.
     */
    buildMetadata = project.getProperty('build_number')
} else {
    buildMetadata = 'SNAPSHOT'
}

String majorAndMinorVersion = "0.6"
Integer patchVersionCode = 1000


android {
    compileSdkVersion sdk
    defaultConfig {
        applicationId "edu.artic"
        minSdkVersion minSdk
        targetSdkVersion sdk
        multiDexEnabled true
        versionCode patchVersionCode
        versionName "$majorAndMinorVersion.$patchVersionCode"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "VERSION_NAME_FOR_DISPLAY", "VERSION_NAME"
    }
    buildTypes {
        debug {
            buildConfigField "String", "VERSION_NAME_FOR_DISPLAY", "\"$majorAndMinorVersion.$patchVersionCode+$buildMetadata\""
            ext.enableCrashlytics = false
            manifestPlaceholders = [crashlyticsEnabled: false]
        }
        release {
            manifestPlaceholders = [crashlyticsEnabled: true]
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation project(':ui')
    implementation project(':db')
    implementation project(':viewmodel')
    implementation project(':base')
    implementation project(':splash')
    implementation project(':welcome')
    implementation project(':tours')
    implementation project(':tour_manager')
    implementation project(':info')
    implementation project(':audio')
    implementation project(':map')
    implementation project(':exhibitions')
    implementation project(':events')
    implementation project(':analytics')
    implementation project(':media')
    implementation project(':media_ui')
    implementation project(':image')
    implementation project(':search')
    implementation project(':details')
    implementation project(':location')
    implementation project(':location_ui')
    implementation project(':membership')
    implementation project(':access_card')
    implementation project(':localization_ui')
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation libs.kotlin
    //Dagger
    implementation libs.dagger
    implementation libs.threeten
    implementation libs.firebase_core
    implementation libs.crashlytics
    kapt libs.dagger_compiler
    implementation libs.dagger_android
    kapt libs.dagger_android_compiler

    testImplementation 'junit:junit:4.12'
    testImplementation 'com.nhaarman:mockito-kotlin:1.5.0'
    // testing uses real threetenbp
    testImplementation('org.threeten:threetenbp:1.3.3') {
        exclude group: 'com.jakewharton.threetenabp', module: 'threetenabp'
    }
}

apply plugin: 'com.google.gms.google-services'