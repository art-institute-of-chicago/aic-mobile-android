// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        sdk = 27
        minSdk = 21
        kotlin_version = '1.2.50'
        latest_helper = '7f1d88469155cedd57df87682806736437088abc'
        dagger_version = '2.15'
        support_version = '27.1.1'
        arch_version = '1.1.0'
        paging_version = '1.0.0-beta1'
        rx_binding_version = '2.1.1'
        kotshi_version = '1.0.4'
        moshi_version = '1.6.0'
        room_version = '1.1.1'
        glide_version = '4.4.0'
        
        libs = [
                kotlin                 : "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version",
                rxHelper               : "com.github.fuzz-productions:rx-helpers:${latest_helper}",
                dagger                 : "com.google.dagger:dagger:${dagger_version}",
                dagger_android         : "com.google.dagger:dagger-android-support:${dagger_version}",
                dagger_compiler        : "com.google.dagger:dagger-compiler:${dagger_version}",
                dagger_android_compiler: "com.google.dagger:dagger-android-processor:${dagger_version}",
                rx                     : 'io.reactivex.rxjava2:rxjava:2.1.7',
                rx_kotlin              : 'io.reactivex.rxjava2:rxkotlin:2.1.0',
                rx_android             : 'io.reactivex.rxjava2:rxandroid:2.0.1',
                arch_runtime           : "android.arch.lifecycle:runtime:${arch_version}",
                arch_extensions        : "android.arch.lifecycle:extensions:${arch_version}",
                arch_paging            : "android.arch.paging:runtime:${paging_version}",
                arch_common            : "android.arch.paging:common:${arch_version}",
                arch_rx                : "android.arch.lifecycle:reactivestreams:${arch_version}",
                retrofit               : 'com.squareup.retrofit2:retrofit:2.3.0',
                retrofit_rx            : 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0',
                retrofit_scalars       : 'com.squareup.retrofit2:converter-scalars:2.1.0',
                retrofit_moshi         : 'com.squareup.retrofit2:converter-moshi:2.1.0',
                moshi                  : "com.squareup.moshi:moshi:${moshi_version}",
                moshi_kotlin_compiler  : "com.squareup.moshi:moshi-kotlin-codegen:${moshi_version}",
                moshi_kotlin           : "com.squareup.moshi:moshi-kotlin:${moshi_version}",
                room                   : "android.arch.persistence.room:runtime:${room_version}",
                room_compiler          : "android.arch.persistence.room:compiler:${room_version}",
                room_rx                : "android.arch.persistence.room:rxjava2:${room_version}",
                timber                 : 'com.jakewharton.timber:timber:4.5.1',
                threeten               : 'com.jakewharton.threetenabp:threetenabp:1.0.5',
                design_support            : "com.android.support:design:${support_version}",
                constraint_layout         : "com.android.support.constraint:constraint-layout:1.1.2",
                glide                     : "com.github.bumptech.glide:glide:${glide_version}",
                glide_compiler: "com.github.bumptech.glide:compiler:${glide_version}"
        ]

    }
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.0-beta03'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

subprojects {
    repositories {
        google()
        jcenter()
        maven { url "https://www.jitpack.io" }
    }

    if (name == 'app') {
        apply plugin: 'com.android.application'
    } else {
        apply plugin: 'com.android.library'
    }
    apply plugin: 'kotlin-android'
    apply plugin: 'kotlin-kapt'

    android {
        compileSdkVersion sdk
        defaultConfig {
            minSdkVersion minSdk
            targetSdkVersion sdk
        }
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_7
            targetCompatibility JavaVersion.VERSION_1_7
        }
        sourceSets {
            main.java.srcDirs += 'src/main/kotlin'
            test.java.srcDirs += 'src/test/kotlin'
        }
    }

    dependencies {
        api libs.kotlin
    }

    kapt {
        useBuildCache = true
        mapDiagnosticLocations = true
        correctErrorTypes = true
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
