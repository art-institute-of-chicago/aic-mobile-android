// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        sdk = 33
        minSdk = 21
        kotlin_version = '1.8.0'
        latest_helper = '72626c5db8'
        dagger_version = '2.19'
        support_version = '28.0.0'
        arch_version = '1.1.1'
        navigation_version = '1.0.0-alpha05'
        paging_version = '1.0.1'
        rx_binding_version = '2.1.1'
        kotshi_version = '1.0.4'
        moshi_version = '1.14.0'
        room_version = '1.1.1'
        glide_version = '4.12.0'
        firebase_version = '16.0.4'
        crashlytics_version = '2.9.5'
        exo_player_version = '2.9.6'

        libs = [
                kotlin                  : "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version",
                kotlin_reflect          : "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version",
                rx_helpers              : "com.github.fuzz-productions:rx-helpers:${latest_helper}",
                dagger                  : "com.google.dagger:dagger:${dagger_version}",
                dagger_android          : "com.google.dagger:dagger-android-support:${dagger_version}",
                dagger_compiler         : "com.google.dagger:dagger-compiler:${dagger_version}",
                dagger_android_compiler : "com.google.dagger:dagger-android-processor:${dagger_version}",
                rx                      : 'io.reactivex.rxjava2:rxjava:2.2.0',
                rx_kotlin               : 'io.reactivex.rxjava2:rxkotlin:2.3.0',
                rx_android              : 'io.reactivex.rxjava2:rxandroid:2.0.1',
                arch_runtime            : 'androidx.lifecycle:lifecycle-runtime-ktx:2.5.1',
                arch_paging             : 'androidx.paging:paging-runtime:2.0.0',
                arch_common             : "android.arch.paging:common:${arch_version}",
                arch_navigation         : 'androidx.navigation:navigation-fragment-ktx:2.5.3',
                arch_navigation_ui      : 'androidx.navigation:navigation-ui-ktx:2.5.3',
                arch_rx                 : 'androidx.lifecycle:lifecycle-reactivestreams:2.0.0',
                retrofit                : 'com.squareup.retrofit2:retrofit:2.3.0',
                retrofit_rx             : 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0',
                retrofit_scalars        : 'com.squareup.retrofit2:converter-scalars:2.1.0',
                retrofit_moshi          : 'com.squareup.retrofit2:converter-moshi:2.1.0',
                retrofit_xml_converter  : "com.squareup.retrofit2:converter-simplexml:2.3.0",
                maps_utils              : "com.google.maps.android:android-maps-utils:0.5",
                moshi                   : "com.squareup.moshi:moshi:${moshi_version}",
                moshi_kotlin_compiler   : "com.squareup.moshi:moshi-kotlin-codegen:${moshi_version}",
                moshi_kotlin            : "com.squareup.moshi:moshi-kotlin:${moshi_version}",
                room                    : 'androidx.room:room-runtime:2.5.0',
                room_compiler           : 'androidx.room:room-compiler:2.5.0',
                room_rx                 : 'androidx.room:room-rxjava2:2.5.0',
                timber                  : 'com.jakewharton.timber:timber:4.5.1',
                threeten                : 'com.jakewharton.threetenabp:threetenabp:1.1.1',
                v4_support              : 'androidx.legacy:legacy-support-v4:1.0.0',
                design_support          : 'com.google.android.material:material:1.0.0',
                constraint_layout       : 'androidx.constraintlayout:constraintlayout:1.1.3',
                glide                   : "com.github.bumptech.glide:glide:${glide_version}",
                glide_compiler          : "com.github.bumptech.glide:compiler:${glide_version}",
                glide_okhttp_integration: "com.github.bumptech.glide:okhttp3-integration:${glide_version}@aar",
                progress_interceptor    : 'com.github.cliabhach:okhttp3-downloadprogress-interceptor:1.0.2-a1',
                maps                    : "com.google.android.gms:play-services-maps:18.1.0",
                location                : "com.google.android.gms:play-services-location:21.0.1",
                exo_player_core         : "com.google.android.exoplayer:exoplayer-core:$exo_player_version",
                exo_player_dash         : "com.google.android.exoplayer:exoplayer-dash:$exo_player_version",
                exo_player_ui           : "com.google.android.exoplayer:exoplayer-ui:$exo_player_version",
                exo_player_media_session: "com.google.android.exoplayer:extension-mediasession:$exo_player_version",
                media_compat            : 'androidx.media:media:1.0.0',
                view_indicator          : "com.github.fuzz-productions:CutoutViewIndicator:v0.8.2",
                custom_tabs             : 'androidx.browser:browser:1.0.0',
                zxing_core              : 'com.google.zxing:core:3.3.0'

        ]
    }
    repositories {
        google()
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.1.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:2.5.3"
        classpath 'com.google.gms:google-services:4.3.15'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

subprojects {

    configurations.all {
        resolutionStrategy {
            force libs.v4_support, libs.design_support, libs.media_compat
        }
    }

    repositories {
        google()
        jcenter()
        maven { url "https://www.jitpack.io" }
        mavenCentral()
    }

    if (name == 'app') {
        apply plugin: 'com.android.application'
        apply plugin: 'com.google.gms.google-services'
    } else {
        apply plugin: 'com.android.library'
    }
    apply plugin: 'kotlin-android'
    // FIXME: Minimize usage of the 'kapt' directive where possible
    // (it can significantly slow down the build process)
    apply plugin: 'kotlin-kapt'

    android {

        buildFeatures {
            viewBinding = true
        }

        ext.envVariable = { String key, boolean isRelease ->
            Properties properties = new Properties()
            File localProperties = rootProject.file('local.properties')
            File releaseProperties = rootProject.file('release.env')
            File devProperties = rootProject.file('debug.env')
            if (isRelease && releaseProperties.exists()) {
                properties.load(releaseProperties.newDataInputStream())
                return properties.getProperty(key)
            } else if (!isRelease && devProperties.exists()) {
                properties.load(devProperties.newDataInputStream())
                return properties.getProperty(key)
            } else if (localProperties.exists()) {
                properties.load(localProperties.newDataInputStream())
                return properties.getProperty(key)
            } else {
                return project.findProperty(key)
            }
        }

        flavorDimensions "version"
        productFlavors {
            store {
                dimension "version"

                buildConfigField "boolean", "IS_RENTAL", "Boolean.parseBoolean(\"false\")"
            }

            rental {
                dimension "version"

                buildConfigField "boolean", "IS_RENTAL", "Boolean.parseBoolean(\"true\")"
            }
        }

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }
        sourceSets {
            main.java.srcDirs += 'src/main/kotlin'
            test.java.srcDirs += 'src/test/kotlin'
        }

        // TODO: Re-enable 'translation' lint check when translated strings arrive
        // TODO: Re-enable 'constraints' lint check whenever the IDE inspection actually agrees with the CLI tool
        // TODO: Re-enable 'sibling' lint check once we've addressed all affected layouts
        lintOptions {
            disable 'MissingTranslation', 'MissingConstraints', 'NotSibling'
        }

        apply plugin: 'androidx.navigation.safeargs'

    }

    dependencies {
        api libs.kotlin
    }

    kapt {
        useBuildCache = true
        mapDiagnosticLocations = true
        correctErrorTypes = true

        // Workaround for https://github.com/google/dagger/issues/1449
        javacOptions {
            option("-source", "8")
            option("-target", "8")
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

repositories {
    google()
}
